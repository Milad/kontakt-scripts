on init
	make_perfview
	set_script_title("Microtuning")
	
	declare $y
	declare %black[5] := (1, 3, 6, 8, 10)
	
	declare ui_switch $C
	declare ui_switch $Db
	declare ui_switch $D
	declare ui_switch $Eb
	declare ui_switch $E
	declare ui_switch $F
	declare ui_switch $Gb
	declare ui_switch $G
	declare ui_switch $Ab
	declare ui_switch $A
	declare ui_switch $Bb
	declare ui_switch $B
	
	declare ui_value_edit $I(-50000, 50000, 1000)
	declare ui_value_edit $II(-50000, 50000, 1000)
	declare ui_value_edit $III(-50000, 50000, 1000)
	declare ui_value_edit $IV(-50000, 50000, 1000)
	declare ui_value_edit $V(-50000, 50000, 1000)
	declare ui_value_edit $VI(-50000, 50000, 1000)
	declare ui_value_edit $VII(-50000, 50000, 1000)
	declare ui_value_edit $VIII(-50000, 50000, 1000)
	declare ui_value_edit $IX(-50000, 50000, 1000)
	declare ui_value_edit $X(-50000, 50000, 1000)
	declare ui_value_edit $XI(-50000, 50000, 1000)
	declare ui_value_edit $XII(-50000, 50000, 1000)
	
	set_text($I, "Do")
	set_text($II, "Re b")
	set_text($III, "Re")
	set_text($IV, "Mi b")
	set_text($V, "Mi")
	set_text($VI, "Fa")
	set_text($VII, "Sol b")
	set_text($VIII, "Sol")
	set_text($IX, "La b")
	set_text($X, "La")
	set_text($XI, "Si b")
	set_text($XII, "Si")
	
	declare ui_menu $preset_menu
	add_menu_item($preset_menu, " ", 99)
	add_menu_item($preset_menu, "Natural", 1)
	add_menu_item($preset_menu, "_____", 99)
	add_menu_item($preset_menu, "Rast", 4)
	add_menu_item($preset_menu, "Huzam", 5)
	add_menu_item($preset_menu, "Hijaz", 6)
	add_menu_item($preset_menu, "Nayruz", 7)
	add_menu_item($preset_menu, "Bayat", 8)
	
	declare $set_start
	$set_start := 220
	
	declare $inter_button_space
	$inter_button_space := 12
	
	$y := 0
	while ($y < 12)
		declare $current_switch
		$current_switch := get_ui_id($C) + $y
		
		declare $current_pos_x
		declare $current_pos_y
		
		if (search(%black, $y mod 12) # -1)
			$current_pos_y := 48
			
			if ($y > 5)
				$current_pos_x := $set_start + 20 + ($inter_button_space * $y)
			else
			 $current_pos_x := $set_start + ($inter_button_space * $y)
			end if
		else
			$current_pos_y := 68
			
			if ($y > 4)
				$current_pos_x := $set_start + 20 + ($inter_button_space * $y)
			else
				$current_pos_x := $set_start + ($inter_button_space * $y)
			end if
		end if
		
		set_control_par($current_switch, $CONTROL_PAR_POS_X, $current_pos_x)
		set_control_par($current_switch, $CONTROL_PAR_POS_Y, $current_pos_y)
		set_control_par($current_switch, $CONTROL_PAR_WIDTH, 13)
		set_control_par($current_switch, $CONTROL_PAR_HEIGHT, 13)
		set_control_par($current_switch, $CONTROL_PAR_TEXT_ALIGNMENT, 1)
		
		inc($y)
	end while
	
	$I := 0
	$II := 0
	$III := 0
	$IV := 0
	$V := 0
	$VI := 0
	$VII := 0
	$VIII := 0
	$IX := 0
	$X := 0
	$XI := 0
	$XII := 0
	
	move_control ($I, 5, 1)
	move_control ($II, 5, 2)
	move_control ($III, 5, 3)
	move_control ($IV, 5, 4)
	move_control ($V, 5, 5)
	move_control ($VI, 5, 6)
	move_control ($VII, 6, 1)
	move_control ($VIII, 6, 2)
	move_control ($IX, 6, 3)
	move_control ($X, 6, 4)
	move_control ($XI, 6, 5)
	move_control ($XII, 6, 6)
	
	move_control ($preset_menu, 1, 3)
	
	make_persistent($C)
	make_persistent($Db)
	make_persistent($D)
	make_persistent($Eb)
	make_persistent($E)
	make_persistent($F)
	make_persistent($Gb)
	make_persistent($G)
	make_persistent($Ab)
	make_persistent($A)
	make_persistent($Bb)
	make_persistent($B)
	
	make_persistent($I)
	make_persistent($II)
	make_persistent($III)
	make_persistent($IV)
	make_persistent($V)
	make_persistent($VI)
	make_persistent($VII)
	make_persistent($VIII)
	make_persistent($IX)
	make_persistent($X)
	make_persistent($XI)
	make_persistent($XII)
	
	make_persistent($preset_menu)
end on

on ui_control ($preset_menu)
	if ($preset_menu < 99)
		{reset everything}
		$y := 0
		while ($y < 12)
			set_control_par(get_ui_id($I) + $y, $CONTROL_PAR_VALUE, 0)
			set_control_par(get_ui_id($C) + $y, $CONTROL_PAR_VALUE, 0)
			inc($y)
		end while
		
		{then set the scale}
		select($preset_menu)
			case 4 {Rast}
				set_control_par(get_ui_id($C) + 4, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 4, $CONTROL_PAR_VALUE, -50000)
				set_control_par(get_ui_id($C) + 11, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 11, $CONTROL_PAR_VALUE, -50000)
			case 5 {Huzam}
				set_control_par(get_ui_id($C), $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I), $CONTROL_PAR_VALUE, -50000)
			case 6 {Hijaz}
				set_control_par(get_ui_id($C) + 9, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 9, $CONTROL_PAR_VALUE, -50000)
			case 7 {Nayruz}
				set_control_par(get_ui_id($C) + 4, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 4, $CONTROL_PAR_VALUE, -50000)
				set_control_par(get_ui_id($C) + 9, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 9, $CONTROL_PAR_VALUE, -50000)
			case 8 {Bayat}
				set_control_par(get_ui_id($C) + 2, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 2, $CONTROL_PAR_VALUE, -50000)
				set_control_par(get_ui_id($C) + 9, $CONTROL_PAR_VALUE, 1)
				set_control_par(get_ui_id($I) + 9, $CONTROL_PAR_VALUE, -50000)
		end select
	end if
end on

on note
	if (get_control_par(get_ui_id($C) + $EVENT_NOTE mod 12, $CONTROL_PAR_VALUE) = 1 and get_control_par(get_ui_id($I) + $EVENT_NOTE mod 12, $CONTROL_PAR_VALUE) # 0)
		change_tune($EVENT_ID, get_control_par(get_ui_id($I) + $EVENT_NOTE mod 12, $CONTROL_PAR_VALUE), 0)
	end if
end on
